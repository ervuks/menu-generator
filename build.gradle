plugins {
    id 'org.springframework.boot' version '2.3.4.RELEASE'
    id 'io.spring.dependency-management' version '1.0.10.RELEASE'
    id 'java'
    id 'jacoco'
    id 'com.palantir.docker' version '0.25.0'
    id 'org.sonarqube' version '2.7'
}

group = 'mavier.food'
version = '1.0'
sourceCompatibility = '11'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

bootJar {
    archiveFileName = 'feeder'
    manifest { attributes "Main-Class": "mavier.food.menu.FoodGeneratorApplication" }
}

task unpack(type: Copy) {
    dependsOn bootJar
    from(zipTree(tasks.bootJar.outputs.files.singleFile))
    into("build/dependency")
}

docker {
    name 'feeder:' + version
    copySpec.from(tasks.unpack.outputs).into("dependency")
    buildArgs(['DEPENDENCY': "dependency"])
    noCache true
}

sonarqube {
}

tasks['sonarqube'].dependsOn test

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled true
    }

    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it,
                    exclude: ['mavier/food/menu/FoodGeneratorApplication**',
                              '**/config/**'
                    ]
            )
        }))
    }
}
test.finalizedBy jacocoTestReport

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-data-mongodb'
    implementation 'org.springframework.boot:spring-boot-starter-test'
    implementation 'org.springframework.boot:spring-boot-starter-security'

    implementation 'org.springdoc:springdoc-openapi-ui:1.4.3'
    implementation 'org.apache.commons:commons-text:1.9'

    compileOnly 'org.projectlombok:lombok'
    runtimeOnly 'org.postgresql:postgresql'
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }

    testImplementation "com.google.truth:truth:1.1"
}

test {
    useJUnitPlatform()
}
